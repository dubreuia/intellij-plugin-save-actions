// Add build script repository to maven central
buildscript {
    repositories {
        mavenCentral()
    }
}

// Add idea plugin version
plugins {
    id "org.jetbrains.intellij" version "0.4.19"
}

// Add idea and java plugins
apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'jacoco'
apply plugin: 'org.jetbrains.intellij'

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileJava {
    options.compilerArgs = ['--release', '8']
}

// Add intellij task configuration for base intellij version (minimum compatibility)
// This needs to fit the tag <idea-version since-build="xxx"> in plugin.xml
// See https://www.jetbrains.com/intellij-repository/snapshots
// See https://www.jetbrains.com/intellij-repository/releases
intellij {
    // From branch 2018.3
    // version '2018.3'
    // From branch 2019.3
    // version '193.3519.25-EAP-SNAPSHOT'
    version 'LATEST-EAP-SNAPSHOT'
    plugins = ['java', 'coverage']
    pluginName 'Save Actions'
    // Do not touch the plugin.xml file
    updateSinceUntilBuild false
}

// Add plugin group and version
group 'com.dubreuia'
version '2.2.0'

// Add only maven central as repository
repositories {
    mavenCentral()
}

// Add dependencies to fest, junit5 api (annotations) and engine (runtime)
dependencies {
    testCompile("org.assertj:assertj-core:3.16.1")
    testCompile 'org.junit.jupiter:junit-jupiter-api:5.6.2'
    testCompile 'org.junit.jupiter:junit-jupiter-params:5.6.2'
    testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.6.2'
}

// Activate junit since gradle 4.7
test {
    useJUnitPlatform()
    systemProperty("idea.force.use.core.classloader", "true")
}

// Add resources directory because intellij test framework checks there for test resources (instead of build/resources)
sourceSets {
    test {
        output.resourcesDir = "build/classes/java/resources"
    }
}

// Add jacoco test report for codecov
jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}
